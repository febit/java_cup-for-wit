
package java_cup.core;

import java_cup.Assoc;
import java_cup.Main;
import java.util.List;
import java_cup.symbol;

terminal
            PACKAGE, IMPORT, SUPER, EXTENDS,
            TERMINAL, NONTERMINAL, PRECEDENCE, NONASSOC, PERCENT_PREC,
            SEMI, COMMA, COLON, COLON_COLON_EQUALS, BAR,
            LEFT, RIGHT, LBRACK, RBRACK, LPAREN, RPAREN, GT, LT,
            QUESTION;

terminal String ID, CODE, CLASSNAME;

nonterminal
            start, varTerm, preced, prodPart;

nonterminal symbol prodPartSym;
nonterminal String  typeName, robustIdent, labid, typearglist, typearguement;

start       ::= (PACKAGE CLASSNAME:name {: Main.packageName = %name%; return null; :}) ?
                (IMPORT CLASSNAME:name {: Main.imports.add(%name%); return null; :})[] ?
                varTerm[]
                preced[] ?
                (ID:lhs COLON_COLON_EQUALS prodPart[][BAR]:rhses SEMI {: createProduction(%lhs%, %rhses%); return null; :})[]
                ;

varTerm     ::= TERMINAL typeName:type ID[COMMA]:symbolList SEMI
                {: declearTerminals(%symbolList%, %type%); return null; :}
                |TERMINAL ID[COMMA]:symbolList SEMI
                {: declearTerminals(%symbolList%, null); return null; :}
                | NONTERMINAL typeName:type ID[COMMA]:symbolList SEMI
                {: declearNonTerminals(%symbolList%, %type%); return null; :}
                | NONTERMINAL ID[COMMA]:symbolList SEMI
                {: declearNonTerminals(%symbolList%, null); return null; :}
                ;


preced      ::= PRECEDENCE LEFT ID[COMMA]:symbolList SEMI
                {: addPrecedence(Assoc.LEFT, %symbolList%); return null; :}
                | PRECEDENCE RIGHT ID[COMMA]:symbolList SEMI
                {: addPrecedence(Assoc.RIGHT, %symbolList%); return null; :}
                | PRECEDENCE NONASSOC ID[COMMA]:symbolList SEMI
                {: addPrecedence(Assoc.NONASSOC, %symbolList%); return null; :}
                ;


prodPartSym ::= ID:sym
                {: return getSymbol(%sym%); :}
                | LPAREN prodPart[][BAR]:rhses RPAREN
                {: return createInsideProductionNonTerminalIfAbsent(%rhses%); :}
                | prodPartSym:sym LBRACK ID:split? RBRACK
                {: return createListNonTerminalIfAbsent(%sym%, %split%); :}
                ;


prodPart    ::= prodPartSym:sym labid:labid?
                {: return createProductionItem(%sym%, %labid%); :} 
                | prodPartSym:sym labid:labid? QUESTION
                {: return createProductionItem(createOptionableNonTerminalIfAbsent(%sym%), %labid%); :}
                | CODE:code
                {: return %code%; :}
                | PERCENT_PREC ID:sym
                {: return getTerminalPrecedence(%sym%); :}
                ;


labid       ::= COLON robustIdent:labid
                {: return %labid%; :}
                ;


typearglist ::= typearguement:arg
                {: return %arg%; :}
                | typearglist:args COMMA typearguement:arg
                {: return (%args%) + ',' + (%arg%); :}
                ;

typearguement ::=
                typeName:id
                {: return %id%;  :}
                | QUESTION
                {: return " ? "; :}
                | QUESTION EXTENDS robustIdent:id
                {: return " ? extends " + (%id%); :}
                | QUESTION SUPER robustIdent:id
                {: return " ? super " + (%id%); :}
                ;

typeName    ::= robustIdent:id
                {: return %id%; :}
                | typeName:id LBRACK RBRACK
                {: return (%id%).concat("[]"); :}
                | robustIdent:id LT typearglist:args GT
                {: return (%id%) + '<' + (%args%) +'>'; :}
                ;


robustIdent ::= ID:the_id	{: return %the_id%; :}
                | TERMINAL	{: return "terminal"; :}
                | NONTERMINAL	{: return "nonterminal"; :}
                | PRECEDENCE	{: return "precedence"; :}
                | LEFT		{: return "left"; :}
                | RIGHT		{: return "right"; :}
                | NONASSOC	{: return "nonassoc"; :}
                ;

